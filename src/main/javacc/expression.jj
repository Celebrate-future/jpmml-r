/*
 * Copyright (c) 2016 Villu Ruusmann
 *
 * This file is part of JPMML-R
 *
 * JPMML-R is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * JPMML-R is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with JPMML-R.  If not, see <http://www.gnu.org/licenses/>.
 */
options {
	JAVA_TEMPLATE_TYPE = "modern";
	JDK_VERSION = "1.5";
	STATIC = false;
}

PARSER_BEGIN(ExpressionTranslator)
package org.jpmml.rexp;

import org.dmg.pmml.Apply;
import org.dmg.pmml.Constant;
import org.dmg.pmml.DataType;
import org.dmg.pmml.Expression;
import org.dmg.pmml.FieldName;
import org.dmg.pmml.FieldRef;

public class ExpressionTranslator {

	static
	public Expression translate(String string){

		try {
			ExpressionTranslator translator = new ExpressionTranslator(string);

			return translator.translateInternal();
		} catch(ParseException pe){
			throw new IllegalArgumentException(pe);
		}
	}

	static
	private String translateFunctionName(String name){

		switch(name){
			case "abs":
				return name;
			case "ceiling":
				return "ceil";
			case "exp":
			case "floor":
				return name;
			case "log":
				return "ln";
			case "log10":
			case "round":
			case "sqrt":
				return name;
			default:
				break;
		}

		switch(name){
			case "is.na":
				return "isMissing";
			default:
				throw new IllegalArgumentException(name);
		}
	}

	static
	private String translateLogicalOperator(String operator){

		switch(operator){
			case "&":
				return "and";
			case "|":
				return "or";
			default:
				throw new IllegalArgumentException(operator);
		}
	}

	static
	private String translateRelationalOperator(String operator){

		switch(operator){
			case "==":
				return "equal";
			case "!=":
				return "notEqual";
			case "<":
				return "lessThan";
			case "<=":
				return "lessOrEqual";
			case ">":
				return "greaterThan";
			case ">=":
				return "greaterOrEqual";
			default:
				throw new IllegalArgumentException(operator);
		}
	}

	static
	private Apply createApply(String function, Expression... expressions){
		Apply apply = new Apply(function);

		for(Expression expression : expressions){
			apply.addExpressions(expression);
		}

		return apply;
	}
}
PARSER_END(ExpressionTranslator)

SKIP:
{
	" "
	|
	"\t"
}

TOKEN:
{
	< MULTIPLY: "*" >
	|
	< DIVIDE: "/" >
	|
	< LPAREN: "(" >
	|
	< RPAREN: ")" >
	|
	< PLUS: "+" >
	|
	< MINUS: "-" >
	|
	< POWER: "^" >
	|
	< POWER_MULTIPLY: "**" >
}

TOKEN:
{
	< AND: "&" >
	|
	< OR: "|" >
	|
	< NOT: "!" >
	|
	< EQUAL: "==" >
	|
	< NOT_EQUAL: "!=" >
	|
	< LESS_THAN: "<" >
	|
	< LESS_OR_EQUAL: "<=" >
	|
	< GREATER_THAN: ">" >
	|
	< GREATER_OR_EQUAL: ">=" >
}

TOKEN:
{
	< IF: "if" >
	|
	< ELSE: "else" >
}

TOKEN:
{
	< FALSE: "FALSE" >
	|
	< TRUE: "TRUE" >
	|
	< NA: "NA" >
}

TOKEN:
{
	< INT: (<DIGIT>)+ (["l", "L"])? >
	|
	< FLOAT:
		(<DIGIT>)+ "." (<DIGIT>)* (<EXP>)?
		|
		"." (<DIGIT>)+ (<EXP>)?
		|
		(<DIGIT>)+ (<EXP>)?
	>
	|
	< IDENTIFIER: <LETTER> (<LETTER> | <DIGIT> | "." | "_")* >
	|
	< STRING:
		"\"" (~["\""])* "\""
		|
		"\'" (~["\'"])* "\'"
	>
	|
	< #DIGIT: ["0" - "9"] >
	|
	< #LETTER: ["a" - "z", "A" - "Z"]>
	|
	< #EXP: ["e", "E"] (["+", "-"])? <INT> >
	|
	< #ESC: "\\" ["\"", "\'", "\\"] >
}

private Expression translateInternal():
{
	Expression expression;
}
{
	expression = Expression() <EOF>

	{
		return expression;
	}
}

Expression Expression():
{
	Expression expression;
}
{
	(
		LOOKAHEAD(<IF>)
		expression = IfExpression()
		|
		expression = LogicalExpression()
	)

	{
		return expression;
	}
}

Expression IfExpression():
{
	Expression condition;
	Expression first;
	Expression second = null;
}
{
	<IF> <LPAREN> condition = Expression() <RPAREN> first = Expression() (LOOKAHEAD(<ELSE>) <ELSE> second = Expression())?

	{
		if(second != null){
			return createApply("if", condition, first, second);
		}

		return createApply("if", condition, first);
	}
}

Expression LogicalExpression():
{
	Expression left;
	Token operator = null;
	Expression right = null;
}
{
	left = RelationalExpression() ((operator = <AND> | operator = <OR>) right = RelationalExpression())?

	{
		if(operator != null){
			return createApply(translateLogicalOperator(operator.image), left, right);
		}

		return left;
	}
}

Expression RelationalExpression():
{
	Expression left;
	Token operator = null;
	Expression right = null;
}
{
	left = NegationExpression() ((operator = <EQUAL> | operator = <NOT_EQUAL> | operator = <LESS_THAN> | operator = <LESS_OR_EQUAL> | operator = <GREATER_THAN> | operator = <GREATER_OR_EQUAL>) right = NegationExpression())?

	{
		if(operator != null){
			return createApply(translateRelationalOperator(operator.image), left, right);
		}

		return left;
	}
}

Expression NegationExpression():
{
	Token operator = null;
	Expression expression;
}
{
	(operator = <NOT>)? expression = AdditiveExpression()

	{
		if(operator != null){
			return createApply("not", expression);
		}

		return expression;
	}
}

Expression AdditiveExpression():
{
	Expression left;
	Token operator = null;
	Expression right = null;
}
{
	left = MultiplicativeExpression() ((operator = <PLUS> | operator = <MINUS>) right = MultiplicativeExpression())?

	{
		if(operator != null){
			return createApply(operator.image, left, right);
		}

		return left;
	}
}

Expression MultiplicativeExpression():
{
	Expression left;
	Token operator = null;
	Expression right = null;
}
{
	left = ExponentiationExpression() ((operator = <MULTIPLY> | operator = <DIVIDE>) right = ExponentiationExpression())?

	{
		if(operator != null){
			return createApply(operator.image, left, right);
		}

		return left;
	}
}

Expression ExponentiationExpression():
{
	Expression left;
	Token operator = null;
	Expression right = null;
}
{
	left = PrimaryExpression() ((operator = <POWER> | operator = <POWER_MULTIPLY>) right = PrimaryExpression())?

	{
		if(operator != null){
			return createApply("pow", left, right);
		}

		return left;
	}
}

Expression PrimaryExpression():
{
	Expression expression;
}
{
	(
		LOOKAHEAD(<IDENTIFIER> <LPAREN>)
		expression = FunctionInvocationExpression()
		|
		expression = FieldInvocationExpression()
		|
		expression = ParenthesizedExpression()
		|
		expression = Constant()
	)

	{
		return expression;
	}
}

Apply FunctionInvocationExpression():
{
	Token name;
	Expression expression;
}
{
	name = <IDENTIFIER> <LPAREN> expression = Expression() <RPAREN>

	{
		return createApply(translateFunctionName(name.image), expression);
	}
}

FieldRef FieldInvocationExpression():
{
	Token name;
}
{
	name = <IDENTIFIER>

	{
		FieldRef fieldRef = new FieldRef()
			.setField(FieldName.create(name.image));

		return fieldRef;
	}
}

Expression ParenthesizedExpression():
{
	Expression expression;
}
{
	<LPAREN> expression = Expression() <RPAREN>

	{
		return expression;
	}
}

Constant Constant():
{
	Token value;
	DataType dataType = null;
}
{
	(
		value = <INT>
		|
		value = <FLOAT> { dataType = DataType.DOUBLE; }
		|
		(value = <FALSE> | value = <TRUE>) { value.image = value.image.toLowerCase(); dataType = DataType.BOOLEAN; }
		|
		value = <STRING> { value.image = value.image.substring(1, value.image.length() - 1); }
	)

	{
		Constant constant = new Constant()
			.setValue(value.image)
			.setDataType(dataType);

		{
			return constant;
		}
	}
}
